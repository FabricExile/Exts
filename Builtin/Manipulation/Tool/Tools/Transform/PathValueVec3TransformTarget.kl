/*
 *  Copyright (c) 2010-2016, Fabric Software Inc. All rights reserved.
 */

require FabricInterfaces;

namespace Tool {

  /// The PathValueVec3TransformTarget specializes BasicXfoTarget
  /// in order to modify from the translation component a Vec3 
  /// external value (Canvas port)
  /// by creating setPathValue commands.
  /// This is used by the :kl-ref:`Tool::PathValueTransformTool`.
  object PathValueVec3TransformTarget : BasicXfoTarget {
    Xfo initTransform;
    SInt32 canMergeID;
    SetPathValueCommandWrapper cmdWrapper;
  };

  /// Implementation of PathValueTarget.
  PathValueVec3TransformTarget( String path ) {
    this.setTargetPath( path );
  }

  /// Implementation of PathValueTarget.
  PathValueVec3TransformTarget.setTargetPath!( String path ) {
    this.cmdWrapper = SetPathValueCommandWrapper( path );
    this.canMergeID = NoCanMergeID;
  }

  String PathValueVec3TransformTarget.getTargetPath() {
    return this.cmdWrapper.getTargetPath();
  }

  /// Implementation of PathValueTarget.
  PathValueVec3TransformTarget.valueChanged!( RTVal val ) {
    Xfo xfo;
    xfo.tr = val;
    this.updateValue( xfo );
  }

  /// Update value from the source port
  PathValueVec3TransformTarget.updateValue!( Xfo transform ) {
    //Call directly parent's method: don't create a command in this case
    //since the value doesn't come from a tool
    this.parent.setLocalTransform( transform );
  }

  /// Implementation of TransformTarget.
  PathValueVec3TransformTarget.setLocalTransform!( Xfo transform ) {
    this.parent.setLocalTransform( transform );
    if( this.canMergeID != NoCanMergeID )
      this.cmdWrapper.setValue( RTVal( this.getLocalTransform().tr ), this.canMergeID, true );//block log during interaction
  }

  /// Implementation of TransformTarget.
  PathValueVec3TransformTarget.beginChanges!( ) {
    this.canMergeID = GetCommandManager().getNewCanMergeID();
  }

  /// Implementation of TransformTarget.
  PathValueVec3TransformTarget.endChanges!( ) {
    this.cmdWrapper.setValue( RTVal( this.getLocalTransform().tr ), this.canMergeID, false );
    this.canMergeID = NoCanMergeID;
  }
}// namespace Tool
