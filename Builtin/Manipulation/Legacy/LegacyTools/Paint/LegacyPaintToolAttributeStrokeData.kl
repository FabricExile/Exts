/*
 *  Copyright (c) 2010-2017 Fabric Software Inc. All rights reserved.
 */

require Geometry;

object LegacyPaintToolAttributeStrokeData {
  Boolean recomputeNormal;
  Ref<GeometryAttribute> attribute;
  GeometryAttribute prevValues;
  GeometryAttribute newValues; 
  UInt32 indexMapping[UInt32];
};

LegacyPaintToolAttributeStrokeData(Ref<Geometry> geometry, String attributeName) {
  this.init(geometry, attributeName, false);
}

LegacyPaintToolAttributeStrokeData(
  Ref<Geometry> geometry, 
  String attributeName, 
  Boolean recomputeNormal) 
{
  this.init(geometry, attributeName, recomputeNormal);
}

protected LegacyPaintToolAttributeStrokeData.init!(
  Ref<Geometry> geometry, 
  String attributeName, 
  Boolean recomputeNormal) 
{
  this.recomputeNormal = recomputeNormal;
  this.indexMapping.clear();
  this.attribute = geometry.getAttributes().getAttribute(attributeName);
  this.prevValues = this.attribute.type().createNew();
  this.newValues = this.attribute.type().createNew();
}

LegacyPaintToolAttributeStrokeData.savePreValue!(Index index) {
  if(!this.indexMapping.has(index))
  {
    this.indexMapping.set(index, this.indexMapping.size());
    this.prevValues.resize(this.indexMapping.size());
    this.newValues.resize(this.indexMapping.size());
    this.prevValues.copyValue(this.attribute, index, this.indexMapping[index]);
  }
}

LegacyPaintToolAttributeStrokeData.saveNewValue!(Index index) {
  this.newValues.copyValue(this.attribute, index, this.indexMapping[index]);
}
