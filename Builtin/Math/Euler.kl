
//
// Copyright (c) 2010-2017 Fabric Software Inc. All rights reserved.
//

/**
The :kl-ref:`Euler` type represents a 3D rotation as Euler X, Y, and Z angles and a :kl-ref:`RotationOrder`. 
The X, Y, and Z angles are expressed in radians. The Euler type also provides conversion methods to :kl-ref:`Mat33`.

\rst
  .. kl-example:: Construct Euler

    RotationOrder ro('xyz');
    Euler euler(0.0, PI, 0.0, ro);
    Mat33 matrix = euler.toMat33();

\endrst
\seealso Quat
\dfgTypeColor Color(0, 191, 232)
\dfgPresetColor Color(99, 129, 92)
*/
struct Euler
{
  Scalar x;
  Scalar y;
  Scalar z;
  RotationOrder ro;
};

/// Default constructor
inline Euler() {
  this.x = 0.0;
  this.y = 0.0;
  this.z = 0.0;
  this.ro.setXYZ();
}

/// Constructor using a Vec3 as angles in radians, RotationOrder = XYZ
inline Euler(in Vec3 angles) {
  this.x = angles.x;
  this.y = angles.y;
  this.z = angles.z;
  this.ro.setXYZ();
}

/// Constructor using a Vec3 as radians and a RotationOrder
inline Euler(in Vec3 angles, in RotationOrder order) {
  this.set(angles, order);
}

/// Constructor using three Scalars as angles in radians, RotationOrder = XYZ
inline Euler(in Scalar x, in Scalar y, in Scalar z) {
  this.set(x, y, z);
}

/// Constructor using three Scalars as angles in radians and a RotationOrder
inline Euler(in Scalar x, in Scalar y, in Scalar z, in RotationOrder order) {
  this.set(x, y, z, order);
}

/// Setter using a Vec3 as angles in radians, RotationOrder = XYZ
inline Euler.set!(in Vec3 angles) {
  this.x = angles.x;
  this.y = angles.y;
  this.z = angles.z;
  this.ro.setXYZ();
}

/// Setter using a Vec3 as radians and a RotationOrder
inline Euler.set!(in Vec3 angles, in RotationOrder order) {
  this.x = angles.x;
  this.y = angles.y;
  this.z = angles.z;
  this.ro = order;
}

/// Setter using three Scalars as angles in radians, RotationOrder = XYZ
inline Euler.set!(in Scalar x, in Scalar y, in Scalar z) {
  this.x = x;
  this.y = y;
  this.z = z;
  this.ro.setXYZ();
}

/// Setter using three Scalars as angles in radians and a RotationOrder
inline Euler.set!(in Scalar x, in Scalar y, in Scalar z, in RotationOrder order) {
  this.x = x;
  this.y = y;
  this.z = z;
  this.ro = order;
}

/// Setter using a Vec3 as angles in radians, RotationOrder = XYZ
inline Euler.setAngles!(in Vec3 angles) {
  this.set(angles);
}

/// Returns the angles as a Vec3 in radians
inline Vec3 Euler.getAngles() {
  return Vec3(this.x, this.y, this.z);
}

/// Sets from Vec.
inline Euler(Vec vec) {
  this.set(vec);
}

/// Sets from Vec.
inline Euler.set!(Vec vec) {
  this.setAngles(Vec3(vec));
}

/// Converts to Vec.
inline Vec Euler.toVec() {
  return this.getAngles().toVec();
}

/// Sets all components of this Euler to 0.0
inline Euler.setNull!() {
  this.x = this.y = this.z = Scalar();
  this.ro = RotationOrder();
}

/// Returns true if this Euler is the same as another one (precisely)
inline Boolean Euler.equal(in Euler other) {
  return this.x == other.x && this.y == other.y && this.z == other.z && this.ro.order == other.ro.order;
}

/// Returns true if this Euler is almost the same as another one (based on a precision)
inline Boolean Euler.almostEqual(in Euler other, in Scalar precision) {
  return
      (abs(this.x - other.x) < precision) &&
      (abs(this.y - other.y) < precision) &&
      (abs(this.z - other.z) < precision) &&
      (this.ro.order == other.ro.order);
}

/// Equals operator
inline Boolean == (Euler a, Euler b) {
  return a.x == b.x && a.y == b.y && a.z == b.z && a.ro.order == b.ro.order;
}

/// Not equals operator
inline Boolean != (Euler a, Euler b) {
  return a.x != b.x || a.y != b.y || a.z != b.z || a.ro.order != b.ro.order;
}

/// Hash for this value
inline UInt32 Euler.hash() {
  return this.x.hash() ^ this.y.hash() ^ this.z.hash() ^ this.ro.order.hash();
}

/// Adds two Eulers
inline Euler + (in Euler a, in Euler b) {
  return Euler(a.x + b.x, a.y + b.y, a.z + b.z, a.ro);
}

/// Adds another Euler to this one
inline Euler. += (in Euler b) {
  this = this + b;
}

/// Subtracts two Eulers
inline Euler - (in Euler a, in Euler b) {
  return Euler(a.x - b.x, a.y - b.y, a.z - b.z, a.ro);
}

/// Subtracts another Euler from this one
inline Euler. -= (in Euler b) {
  this = this - b;
}

/// unary -
inline Euler -Euler() {
  return Euler(-this.x, -this.y, -this.z);
}

/// Converts the Euler to a Mat33 representing 
/// the same 3D rotation transformation
function Mat33 Euler.toMat33() {
  Scalar Cx = cos(this.x), Sx = sin(this.x);
  Scalar Cy = cos(this.y), Sy = sin(this.y);
  Scalar Cz = cos(this.z), Sz = sin(this.z);

  Mat33 Rx( 1.0, 0.0, 0.0,
            0.0,  Cx, -Sx,
            0.0,  Sx,  Cx);

  Mat33 Ry(  Cy,  0.0,  Sy,
            0.0,  1.0, 0.0,
            -Sy,  0.0,  Cy);

  Mat33 Rz(  Cz,  -Sz,  0.0,
             Sz,   Cz,  0.0,
            0.0,  0.0,  1.0);

  Mat33 result;

  if (this.ro.isZYX())
    result = Rx * Ry * Rz;
  else if (this.ro.isXZY())
    result = Ry * Rz * Rx;
  else if (this.ro.isYXZ())
    result = Rz * Rx * Ry;
  else if (this.ro.isYZX())
    result = Rx * Rz * Ry;
  else if (this.ro.isXYZ())
    result = Rz * Ry * Rx;
  else if (this.ro.isZXY())
    result = Ry * Rx * Rz;

  return result;
}


