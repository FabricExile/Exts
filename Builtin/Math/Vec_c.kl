//
// Copyright (c) 2010-2017 Fabric Software Inc. All rights reserved.
//
/**
The :kl-ref:`Vec_c` represents a dense vector.

\rst
  .. kl-example:: Vec_c

    // create a 7-element vector
    Vec_c vec(7);

\endrst

\dfgTypeColor Color(249, 157, 28)
\dfgPresetColor Color(99, 129, 92)
*/

struct Vec_c {
  private Complex vals[];
};

/// Construct a vector with ``size`` elements
inline Vec_c(Integer size) {
  this.vals.resize(size);
}

/// Construct a vector with predefined values
inline Vec_c(Complex vals[]) {
  this.vals = vals.clone();
}

/// Construct a vector from the vector ``that``
inline Vec_c(Vec_c that) {
  this.vals = that.vals.clone();
}

/// Sets from Vec.
inline Vec_c.set!(Vec_c vec) {
  this.vals = vec.vals.clone();
}

/// Converts to Vec.
inline Vec_c Vec_c.toVec() {
  return this;
}

/// Get the size of the vector
inline Integer Vec_c.size() {
  return this.vals.size();
}

/// Return all of the values
inline Complex[] Vec_c.getValues() {
  return this.vals.clone();
}

/// In Fabric guarded mode this method throws an exception if the size
/// of the current vector differs from the size of the ``that`` vector
inline Integer Vec_c.validateSize(Vec_c that, String desc) {
  Integer thisSize = this.size();
  if (Fabric_Guarded) {
    Integer thatSize = that.size();
    if (thatSize != thisSize)
      throw desc + ": incompatible sizes (" + thisSize + " vs. " + thatSize + ")";
  }
  return thisSize;
}

/// Get value of the vector element at the ``ind`` position
inline Complex Vec_c.get(Integer ind) {
  return this.vals[ind];
}

/// Change the number of elements in the vector to ``newSize``
inline Vec_c.resize!(Integer newSize) {
  this.vals.resize(newSize);
}

/// Set the value of element ``ind`` to ``val``
inline Vec_c.set!(Integer ind, Complex val) {
  this.vals[ind] = val;
}

/// Inserts a value ``val`` at index ``ind``.
/// Resize the vector accordingly if ``ind`` is superior to its size.
inline Vec_c.insert!(Integer ind, Complex val) {
  this.vals.resize( ind > this.size()  ? ind + 1 : this.size()+1);
  for(Index i=this.size()-1; i>ind; i--)
    this.vals[i] = this.vals[i-1];
  this.vals[ind] = val;
}

/// Remove and retuens the value at index ``ind``
/// resize the vector.
inline Complex Vec_c.remove!(Integer ind) {
  Complex temp = this.vals[ind];
  for(Index i=ind; i<this.size()-1; i++)
    this.vals[i] = this.vals[i+1];
  this.vals.resize(this.size()-1);
  return temp;
}

inline Vec_c.push_top!(Complex val) {
  this.insert(0, val);
}

inline Vec_c.push_back!(Complex val) {
  this.insert(this.size(), val);
}

inline Complex Vec_c.pop_top!() {
  return this.remove(0);
}

inline Complex Vec_c.pop_back!() {
  return this.remove(this.size()-1);
}

/// Return a String description of this vector and its size
inline String Vec_c.sizeDesc() {
  return "Vec_c(" + this.size() + ")";
}

/// Append a String description of this vector's size and contents
/// to the io variable ``string``
Vec_c.appendDesc(io String string) {
  string += this.sizeDesc();
  string += ":[";
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind) {
    if (ind > 0)
      string += " ";
    string += this.get(ind);
  }
  string += "]";
}

/// Set the value of all vector elements to ``val``
inline Vec_c.setAll!(Complex val) {
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, val);
}

/// Set the value of all vector elements to zero
inline Vec_c.setNull!() {
  this.setAll(Complex());
}

/// Returns true if this vector is the same as another one
inline Boolean Vec_c.equal(Vec_c that) {
  for (Integer i = 0; i < this.vals.size(); ++i)
    if (this.vals[i] != that.vals[i])
      return false;
  return true;
}

/// Returns true if this vector is almost the same as another one
/// (using a default precision)
inline Boolean Vec_c.almostEqual(
  Vec_c that,
  Scalar precision
  )
{
  for (Integer i = 0; i < this.vals.size(); ++i)
    if (!this.vals[i].almostEqual(that.vals[i], precision))
      return false;
  return true;
}

/// Returns true if this vector is almost the same as another one
/// (using a default precision)
inline Boolean Vec_c.almostEqual(in Vec_c other) {
  return this.almostEqual(other, PRECISION);
}

/// Equals operator
inline Boolean == (Vec_c lhs, Vec_c rhs) {
  return lhs.equal(rhs);
}

/// Not equals operator
inline Boolean != (Vec_c lhs, Vec_c rhs) {
  return !lhs.equal(rhs);
}

/// Hash for this matrix
inline UInt32 Vec_c.hash() {
  UInt32 hash;
  for (Integer ind = 0; ind < this.vals.size(); ++ind)
    hash ^= this.get(ind).hash();
  return hash;
}

/// Returns the addition of two matrices
inline Vec_c +(Vec_c a, Vec_c b) {
  Integer size = a.validateSize(b, FUNC);
  Vec_c result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, a.get(ind) + b.get(ind));
  return result;
}

/// Add another matrix in place
inline Vec_c.+=(Vec_c that) {
  Integer size = this.validateSize(that, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) + that.get(ind));
}

/// Returns the addition of two matrices
inline Vec_c -(Vec_c a, Vec_c b) {
  Integer size = a.validateSize(b, FUNC);
  Vec_c result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, a.get(ind) - b.get(ind));
  return result;
}

/// Add another matrix in place
inline Vec_c.-=(Vec_c that) {
  Integer size = this.validateSize(that, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) - that.get(ind));
}

/// unary -
inline Vec_c -Vec_c() {
  Integer size = this.size();
  Vec_c res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, -this.get(ind));
  return res;
}

inline Vec_c *(Vec_c lhs, Complex rhs) {
  Integer size = lhs.size();
  Vec_c res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) * rhs);
  return res;
}

inline Vec_c *(Complex lhs, Vec_c rhs) {
  Integer size = rhs.size();
  Vec_c result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, lhs * rhs.get(ind));
  return result;
}

inline Vec_c.*=(Complex val) {
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) * val);
}

inline Vec_c /(Vec_c lhs, Complex rhs) {
  Integer size = lhs.size();
  Vec_c res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) / rhs);
  return res;
}

inline Vec_c /(Complex lhs, Vec_c rhs) {
  Integer size = rhs.size();
  Vec_c result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, lhs / rhs.get(ind));
  return result;
}

inline Vec_c./=(Complex val) {
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) / val);
}

inline Vec_c *(Vec_c lhs, Vec_c rhs) {
  Integer size = lhs.validateSize(rhs, FUNC);
  Vec_c res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) * rhs.get(ind));
  return res;
}

inline Vec_c.*=(Vec_c val) {
  Integer size = this.validateSize(val, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) * val.get(ind));
}

inline Vec_c /(Vec_c lhs, Vec_c rhs) {
  Integer size = lhs.validateSize(rhs, FUNC);
  Vec_c res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) / rhs.get(ind));
  return res;
}

inline Vec_c./=(Vec_c val) {
  Integer size = this.validateSize(val, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) / val.get(ind));
}

inline Complex Vec_c.dot(Vec_c that) {
  Integer size = this.validateSize(that, FUNC);
  Complex res;
  for (Integer ind = 0; ind < size; ++ind)
    res += this.get(ind) * that.get(ind);
  return res;
}

inline Complex Vec_c.lengthSquared() {
  Integer size = this.size();
  Complex res;
  for (Integer ind = 0; ind < size; ++ind) {
    Complex val = this.get(ind);
    res += val * val;
  }
  return res;
}

/// Return the length of the vector
inline Complex Vec_c.length() {
  return sqrt(this.lengthSquared());
}

/// Returns the unit vector of this one, throws and exception if almost zero length
inline Vec_c Vec_c.unit() {
  Complex len = this.length();
  if (Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor(len))
    Math_reportBadDivisor(len, FUNC);
  return this / len;
}

/// Sets this vector to its unit vector
/// and returns its previous length
inline Complex Vec_c.setUnit!() {
  Complex len = this.length();
  if (Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor(len))
    Math_reportBadDivisor(len, FUNC);
  this /= len;
  return len;
}

/// Mormalizes the vector, same as setUnit.
inline Vec_c.normalize!() {
  this.setUnit();
}


/// Linearly interpolates this vector with another one
/// based on a scalar blend value (0.0 to 1.0)
inline Vec_c Vec_c.linearInterpolate(Vec_c that, Complex t) {
  return this * (1 - t) + that * t;
}

// Return a subvector of elements in the range [s, e)
inline Vec_c Vec_c.sub(Integer s, Integer e) {
  if (Fabric_Guarded) {
    if (s > e)
      throw FUNC + ": must have s <= e";
    if (e > this.size())
      throw FUNC + ": must have e <= this.size()";
  }
  Integer n = e - s;
  Vec_c res(n);
  for (Integer i = 0; i < n; ++i)
    res.set(i, this.get(s + i));
  return res;
}

// Return first part of vector
inline Vec_c Vec_c.head(Integer n) {
  return this.sub(0, n);
}

inline Complex Vec_c.minimum() {
  Complex smallest = this.get(0);
  for (UInt32 i = 1; i < this.size(); i++) 
    if (this.get(i) < smallest) smallest = this.get(i);
  return smallest;
}

inline Complex Vec_c.maximum() {
  Complex biggest = this.get(0);
  for (UInt32 i = 1; i < this.size(); i++) 
    if (this.get(i) > biggest) biggest = this.get(i);
  return biggest;
}

inline Complex Vec_c.sum() {
  Complex result;
  for (UInt32 i = 0; i < this.size(); i++)
      result += this.get(i);
  return result;
}
