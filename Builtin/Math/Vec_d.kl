//
// Copyright (c) 2010-2017 Fabric Software Inc. All rights reserved.
//
/**
The :kl-ref:`Vec_d` represents a dense vector.

\rst
  .. kl-example:: Vec_d

    // create a 7-element vector
    Vec_d vec(7);

\endrst

\dfgTypeColor Color(249, 157, 28)
\dfgPresetColor Color(99, 129, 92)
*/

struct Vec_d {
  private Float64 vals[];
};

/// Construct a vector with ``size`` elements
inline Vec_d(Integer size) {
  this.vals.resize(size);
}

/// Construct a vector with predefined values
inline Vec_d(Float64 vals[]) {
  this.vals = vals.clone();
}

/// Construct a vector from the vector ``that``
inline Vec_d(Vec_d that) {
  this.vals = that.vals.clone();
}

/// Sets from Vec.
inline Vec_d.set!(Vec_d vec) {
  this.vals = vec.vals.clone();
}

/// Converts to Vec.
inline Vec_d Vec_d.toVec() {
  return this;
}

/// Get the size of the vector
inline Integer Vec_d.size() {
  return this.vals.size();
}

/// Return all of the values
inline Float64[] Vec_d.getValues() {
  return this.vals.clone();
}

/// In Fabric guarded mode this method throws an exception if the size
/// of the current vector differs from the size of the ``that`` vector
inline Integer Vec_d.validateSize(Vec_d that, String desc) {
  Integer thisSize = this.size();
  if (Fabric_Guarded) {
    Integer thatSize = that.size();
    if (thatSize != thisSize)
      throw desc + ": incompatible sizes (" + thisSize + " vs. " + thatSize + ")";
  }
  return thisSize;
}

/// Get value of the vector element at the ``ind`` position
inline Float64 Vec_d.get(Integer ind) {
  return this.vals[ind];
}

/// Change the number of elements in the vector to ``newSize``
inline Vec_d.resize!(Integer newSize) {
  this.vals.resize(newSize);
}

/// Set the value of element ``ind`` to ``val``
inline Vec_d.set!(Integer ind, Float64 val) {
  this.vals[ind] = val;
}

/// Inserts a value ``val`` at index ``ind``.
/// Resize the vector accordingly if ``ind`` is superior to its size.
inline Vec_d.insert!(Integer ind, Float64 val) {
  this.vals.resize( ind > this.size()  ? ind + 1 : this.size()+1);
  for(Index i=this.size()-1; i>ind; i--)
    this.vals[i] = this.vals[i-1];
  this.vals[ind] = val;
}

/// Remove and retuens the value at index ``ind``
/// resize the vector.
inline Float64 Vec_d.remove!(Integer ind) {
  Float64 temp = this.vals[ind];
  for(Index i=ind; i<this.size()-1; i++)
    this.vals[i] = this.vals[i+1];
  this.vals.resize(this.size()-1);
  return temp;
}

inline Vec_d.push_top!(Float64 val) {
  this.insert(0, val);
}

inline Vec_d.push_back!(Float64 val) {
  this.insert(this.size(), val);
}

inline Float64 Vec_d.pop_top!() {
  return this.remove(0);
}

inline Float64 Vec_d.pop_back!() {
  return this.remove(this.size()-1);
}

/// Return a String description of this vector and its size
inline String Vec_d.sizeDesc() {
  return "Vec_d(" + this.size() + ")";
}

/// Append a String description of this vector's size and contents
/// to the io variable ``string``
Vec_d.appendDesc(io String string) {
  string += this.sizeDesc();
  string += ":[";
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind) {
    if (ind > 0)
      string += " ";
    string += this.get(ind);
  }
  string += "]";
}

/// Set the value of all vector elements to ``val``
inline Vec_d.setAll!(Float64 val) {
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, val);
}

/// Set the value of all vector elements to zero
inline Vec_d.setNull!() {
  this.setAll(Float64());
}

/// Returns true if this vector is the same as another one
inline Boolean Vec_d.equal(Vec_d that) {
  for (Integer i = 0; i < this.vals.size(); ++i)
    if (this.vals[i] != that.vals[i])
      return false;
  return true;
}

/// Returns true if this vector is almost the same as another one
/// (using a default precision)
inline Boolean Vec_d.almostEqual(
  Vec_d that,
  Float64 precision
  )
{
  for (Integer i = 0; i < this.vals.size(); ++i)
    if (abs(this.vals[i] - that.vals[i]) >= precision)
      return false;
  return true;
}

/// Returns true if this vector is almost the same as another one
/// (using a default precision)
inline Boolean Vec_d.almostEqual(in Vec_d other) {
  return this.almostEqual(other, PRECISION);
}

/// Equals operator
inline Boolean == (Vec_d lhs, Vec_d rhs) {
  return lhs.equal(rhs);
}

/// Not equals operator
inline Boolean != (Vec_d lhs, Vec_d rhs) {
  return !lhs.equal(rhs);
}

/// Hash for this matrix
inline UInt32 Vec_d.hash() {
  UInt32 hash;
  for (Integer ind = 0; ind < this.vals.size(); ++ind)
    hash ^= this.get(ind).hash();
  return hash;
}

/// Returns the addition of two matrices
inline Vec_d +(Vec_d a, Vec_d b) {
  Integer size = a.validateSize(b, FUNC);
  Vec_d result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, a.get(ind) + b.get(ind));
  return result;
}

/// Add another matrix in place
inline Vec_d.+=(Vec_d that) {
  Integer size = this.validateSize(that, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) + that.get(ind));
}

/// Returns the addition of two matrices
inline Vec_d -(Vec_d a, Vec_d b) {
  Integer size = a.validateSize(b, FUNC);
  Vec_d result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, a.get(ind) - b.get(ind));
  return result;
}

/// Add another matrix in place
inline Vec_d.-=(Vec_d that) {
  Integer size = this.validateSize(that, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) - that.get(ind));
}

/// unary -
inline Vec_d -Vec_d() {
  Integer size = this.size();
  Vec_d res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, -this.get(ind));
  return res;
}

inline Vec_d *(Vec_d lhs, Float64 rhs) {
  Integer size = lhs.size();
  Vec_d res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) * rhs);
  return res;
}

inline Vec_d *(Float64 lhs, Vec_d rhs) {
  Integer size = rhs.size();
  Vec_d result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, lhs * rhs.get(ind));
  return result;
}

inline Vec_d.*=(Float64 val) {
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) * val);
}

inline Vec_d /(Vec_d lhs, Float64 rhs) {
  Integer size = lhs.size();
  Vec_d res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) / rhs);
  return res;
}

inline Vec_d /(Float64 lhs, Vec_d rhs) {
  Integer size = rhs.size();
  Vec_d result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, lhs / rhs.get(ind));
  return result;
}

inline Vec_d./=(Float64 val) {
  Integer size = this.size();
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) / val);
}

inline Vec_d *(Vec_d lhs, Vec_d rhs) {
  Integer size = lhs.validateSize(rhs, FUNC);
  Vec_d res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) * rhs.get(ind));
  return res;
}

inline Vec_d.*=(Vec_d val) {
  Integer size = this.validateSize(val, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) * val.get(ind));
}

inline Vec_d /(Vec_d lhs, Vec_d rhs) {
  Integer size = lhs.validateSize(rhs, FUNC);
  Vec_d res(size);
  for (Integer ind = 0; ind < size; ++ind)
    res.set(ind, lhs.get(ind) / rhs.get(ind));
  return res;
}

inline Vec_d./=(Vec_d val) {
  Integer size = this.validateSize(val, FUNC);
  for (Integer ind = 0; ind < size; ++ind)
    this.set(ind, this.get(ind) / val.get(ind));
}

inline Float64 Vec_d.dot(Vec_d that) {
  Integer size = this.validateSize(that, FUNC);
  Float64 res;
  for (Integer ind = 0; ind < size; ++ind)
    res += this.get(ind) * that.get(ind);
  return res;
}

inline Float64 Vec_d.lengthSquared() {
  Integer size = this.size();
  Float64 res;
  for (Integer ind = 0; ind < size; ++ind) {
    Float64 val = this.get(ind);
    res += val * val;
  }
  return res;
}

/// Return the length of the vector
inline Float64 Vec_d.length() {
  return sqrt(this.lengthSquared());
}

/// Returns the unit vector of this one, throws and exception if almost zero length
inline Vec_d Vec_d.unit() {
  Float64 len = this.length();
  if (Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor(len))
    Math_reportBadDivisor(len, FUNC);
  return this / len;
}

/// Sets this vector to its unit vector
/// and returns its previous length
inline Float64 Vec_d.setUnit!() {
  Float64 len = this.length();
  if (Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor(len))
    Math_reportBadDivisor(len, FUNC);
  this /= len;
  return len;
}

/// Mormalizes the vector, same as setUnit.
inline Vec_d.normalize!() {
  this.setUnit();
}

/// Clamps this vector per component by a min and max vector
inline Vec_d Vec_d.clamp(Vec_d min, Vec_d max) {
  Integer size = this.validateSize(min, FUNC);
  this.validateSize(max, FUNC);
  Vec_d result(size);
  for (Integer ind = 0; ind < size; ++ind)
    result.set(ind, Math_clamp(this.get(ind), min.get(ind), max.get(ind)));
  return result;
}

/// Linearly interpolates this vector with another one
/// based on a scalar blend value (0.0 to 1.0)
inline Vec_d Vec_d.linearInterpolate(Vec_d that, Float64 t) {
  return this * (1 - t) + that * t;
}

// Return a subvector of elements in the range [s, e)
inline Vec_d Vec_d.sub(Integer s, Integer e) {
  if (Fabric_Guarded) {
    if (s > e)
      throw FUNC + ": must have s <= e";
    if (e > this.size())
      throw FUNC + ": must have e <= this.size()";
  }
  Integer n = e - s;
  Vec_d res(n);
  for (Integer i = 0; i < n; ++i)
    res.set(i, this.get(s + i));
  return res;
}

// Return first part of vector
inline Vec_d Vec_d.head(Integer n) {
  return this.sub(0, n);
}

inline Float64 Vec_d.minimum() {
  Float64 smallest = this.get(0);
  for (UInt32 i = 1; i < this.size(); i++) 
    if (this.get(i) < smallest) smallest = this.get(i);
  return smallest;
}

inline Float64 Vec_d.maximum() {
  Float64 biggest = this.get(0);
  for (UInt32 i = 1; i < this.size(); i++) 
    if (this.get(i) > biggest) biggest = this.get(i);
  return biggest;
}

inline Float64 Vec_d.sum() {
  Float64 result;
  for (UInt32 i = 0; i < this.size(); i++)
      result += this.get(i);
  return result;
}
