//
// Copyright (c) 2010-2017 Fabric Software Inc. All rights reserved.
//
/**
The :kl-ref:`Quat` represents a 3D rotation as a quaternion. Quaternions are used within the :kl-ref:`Xfo` type. A :kl-ref:`Quat` can be converted to and from :kl-ref:`Mat33`  as well as :kl-ref:`Euler` types.

\rst
  .. kl-example:: Quat

    // create several quaternions
    Quat q0, q1, q2, q3, q4;
    q0 = Quat(Vec3(1.0, 0.0, 0.0), 0.0);
    q1.setFromEuler(Euler(PI, 0.0, 0.0));
    q2.setFromDirectionAndUpvector(Vec3(1.0, 0.0, 0.0), Vec3(0.0, 1.0, 0.0));
    q3.setFrom2Vectors(Vec3(1.0, 0.0, 0.0), Vec3(1.0, 1.0, 0.0));
    q4.setFromAxisAndAngle(Vec3(0.0, 1.0, 0.0), HALF_PI);

\endrst
\seealso Xfo, Euler, Mat33
\dfgTypeColor Color(0, 191, 232)
\dfgPresetColor Color(99, 129, 92)
*/

struct Quat
{
  Vec3 v;
  Scalar w;
};

/// Default constructor - set to identity
inline Quat() {
  this.setIdentity();
}

/// Construct from scalars
/// \dfgPresetDefault w 1
inline Quat(in Scalar x, in Scalar y, in Scalar z, in Scalar w) {
  this.v.set(x, y, z);
  this.w = w;
}

/// Construct from scalar and vector
inline Quat(in Scalar w, in Vec3 v) {
  this.v = v;
  this.w = w;
}

/// Construct from vector and scalar
inline Quat(in Vec3 v, in Scalar w) {
  this.v = v;
  this.w = w;
}

/// Construct from euler angles
inline Quat(in Euler e) {
  this.setFromEuler(e);
}

/// Construct from Mat33
inline Quat(in Mat33 mat) {
  this.setFromMat33(mat);
}

/// Sets each component with a single scalar value.
inline Quat.set!(in Scalar x, in Scalar y, in Scalar z, in Scalar w) {
  this.v.set(x, y, z);
  this.w = w;
}

/// Setter from scalar and vector
inline Quat.set!(in Scalar w, in Vec3 v) {
  this.v = v;
  this.w = w;
}

/// Setter from vector and scalar
inline Quat.set!(in Vec3 v, in Scalar w) {
  this.v = v;
  this.w = w;
}

/// Setter from (standard/external) float32 array and an offset
inline Quat.set!(Float32 v<>, UInt32 o) {
  if(v.size() >= 4 + o) {
    this.v.set(v, o);
    this.w = v[o + 3];
  }
}
/// Getter as external float32 array and an offset
inline Quat.get(io Float32 v<>, UInt32 o) {
  if(v.size() >= 4 + o) {
    this.v.get(v, o);
    v[o + 3] = Float32(this.w);
  }
}

/// Getter as float32 array and an offset
inline Quat.get(io Float32 v[], UInt32 o) {
  if(v.size() < 4 + o)
    v.resize(4 + o);
  this.v.get(v, o);
  v[o + 3] = Float32(this.w);
}

/// Setter from (standard/external) float64 array and an offset
inline Quat.set!(Float64 v<>, UInt32 o) {
  if(v.size() >= 4 + o) {
    this.v.set(v, o);
    this.w = Float32(v[o + 3]);
  }
}

/// Getter as external float64 array and an offset
inline Quat.get(io Float64 v<>, UInt32 o) {
  if(v.size() >= 4 + o) {
    this.v.get(v, o);
    v[o + 3] = this.w;
  }
}

/// Getter as float64 array and an offset
inline Quat.get(io Float64 v[], UInt32 o) {
  if(v.size() < 4 + o)
    v.resize(4 + o);
  this.v.get(v, o);
  v[o + 3] = this.w;
}

/// Setter from (standard/external) float32 array
inline Quat.set!(Float32 v<>) {
  this.set(v, 0);
}

/// Getter as external float32 array
inline Quat.get(io Float32 v<>) {
  this.get(v, 0);
}

/// Getter as float32 array
inline Quat.get(io Float32 v[]) {
  this.get(v, 0);
}

/// Setter from (standard/external) float64 array
inline Quat.set!(Float64 v<>) {
  this.set(v, 0);
}

/// Getter as external float64 array
inline Quat.get(io Float64 v<>) {
  this.get(v, 0);
}

/// Getter as float64 array
inline Quat.get(io Float64 v[]) {
  this.get(v, 0);
}

/// Sets from Vec.
inline Quat(Vec vec) {
  this.set(vec);
}

/// Sets from Vec.
inline Quat.set!(Vec vec) {
  this.v.x = vec.get(0);
  this.v.y = vec.get(1);
  this.v.z = vec.get(2);
  this.w = vec.get(3);
}

/// Converts to Vec.
inline Vec Quat.toVec() {
  Vec vec(4);
  vec.set(0, this.v.x);
  vec.set(1, this.v.y);
  vec.set(2, this.v.z);
  vec.set(3, this.w);
  return vec;
}

/// Set this quat to the identity
inline Quat.setIdentity!() {
  this.v = Vec3(0.0, 0.0, 0.0);
  this.w = 1.0;
}

/// Set this quat from a euler rotation
function Quat Quat.setFromEuler!(in Euler e) {
  Vec3 ordered;
  if (e.ro.isXYZ()) {
    ordered.set(e.z,e.y,e.x);
  }
  else if (e.ro.isYZX()) {
    ordered.set(e.x,e.z,e.y);
  }
  else if (e.ro.isZXY()) {
    ordered.set(e.y,e.x,e.z);
  }
  else if (e.ro.isXYZ()) {
    ordered.set(e.y,-e.z,e.x);
  }
  else if (e.ro.isZYX()) {
    ordered.set(e.x,-e.y,e.z);
  }
  else if (e.ro.isYXZ()) {
    ordered.set(e.z,-e.x,e.y);
  }

  Scalar ti, tj, tk;
  ti = ordered.x * 0.5;
  tj = ordered.y * 0.5;
  tk = ordered.z * 0.5;
  Scalar ci = cos(ti), cj = cos(tj), ck = cos(tk);
  Scalar si = sin(ti), sj = sin(tj), sk = sin(tk);
  Scalar cc = ci * ck, cs = ci * sk, sc = si * ck, ss = si * sk;
  Scalar ai, aj, ak;
  ai = cj * sc - sj * cs;
  aj = cj * ss + sj * cc;
  ak = cj * cs - sj * sc;

  this.w = cj * cc + sj * ss;

  if (e.ro.isXYZ()) {
    this.v.x = ak; this.v.y = aj; this.v.z = ai;
  }
  else if (e.ro.isYZX()) {
    this.v.x = ai; this.v.y = ak; this.v.z = aj;
  }
  else if (e.ro.isZXY()) {
    this.v.x = aj; this.v.y = ai; this.v.z = ak;
  }
  else if (e.ro.isXZY()) {
    this.v.x = ak; this.v.y = ai; this.v.z = -aj;
  }
  else if (e.ro.isZYX()) {
    this.v.x = ai; this.v.y = -aj; this.v.z = ak;
  }
  else if (e.ro.isYXZ()) {
    this.v.x = -aj; this.v.y = ak; this.v.z = ai;
  }
  return this;
}

/// Set this quat to a given angles vector (in radians) and a rotation order
inline Quat Quat.setFromEulerAngles!(Vec3 angles, RotationOrder ro) {
  Euler euler(angles, ro);
  return this.setFromEuler(euler);
}

/// Set this quat to a given angles vector (in radians) using
/// the default XYZ rotation order
inline Quat Quat.setFromEulerAngles!(Vec3 angles) {
  return this.setFromEulerAngles(angles, RotationOrder(0));
}

/// Set this quat to a given angles vector (in radians) and a rotation order
/// \dfgPresetRange eulerX (-3.14, 3.14)
/// \dfgPresetRange eulerY (-3.14, 3.14)
/// \dfgPresetRange eulerZ (-3.14, 3.14)
inline Quat Quat.setFromEulerAngles!(Scalar eulerX, Scalar eulerY, Scalar eulerZ, RotationOrder ro) {
  Euler euler(Vec3(eulerX, eulerY, eulerZ), ro);
  return this.setFromEuler(euler);
}

/// Set this quat to a given angles vector (in radians) using
/// the default XYZ rotation order
/// \dfgPresetRange eulerX (-3.14, 3.14)
/// \dfgPresetRange eulerY (-3.14, 3.14)
/// \dfgPresetRange eulerZ (-3.14, 3.14)
inline Quat Quat.setFromEulerAngles!(Scalar eulerX, Scalar eulerY, Scalar eulerZ) {
  return this.setFromEulerAngles(Vec3(eulerX, eulerY, eulerZ), RotationOrder(0));
}

/// Set this quat to a rotation defined by an axis and an angle (in radians)
inline Quat Quat.setFromAxisAndAngle!(in Vec3 axis, in Scalar angle) {
  Scalar halfAngle = angle / 2.0;
  this.set(cos(halfAngle), sin(halfAngle) * axis.unit());
  return this;
}

/// Set this quat to the rotation described by a
/// 3x3 rotation matrix
function Quat Quat.setFromMat33!(in Mat33 mat) {
  Scalar trace = mat.row0.x + mat.row1.y + mat.row2.z;
  if (trace > 0) {
    Scalar s = 2.0 * sqrt(trace + 1.0);
    this.w = 0.25 * s;
    if( Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor( s ) )
      Math_reportBadDivisor( s, "Quat.setFromMat33" );
    Scalar invS = 1.0 / s;
    this.v.x = (mat.row2.y - mat.row1.z) * invS;
    this.v.y = (mat.row0.z - mat.row2.x) * invS;
    this.v.z = (mat.row1.x - mat.row0.y) * invS;
  }
  else if (mat.row0.x > mat.row1.y && mat.row0.x > mat.row2.z) {
    Scalar s = 2.0 * sqrt(1.0 + mat.row0.x - mat.row1.y - mat.row2.z);
    if( Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor( s ) )
      Math_reportBadDivisor( s, "Quat.setFromMat33" );
    Scalar invS = 1.0 / s;
    this.w = (mat.row2.y - mat.row1.z) * invS;
    this.v.x = 0.25 * s;
    this.v.y = (mat.row0.y + mat.row1.x) * invS;
    this.v.z = (mat.row0.z + mat.row2.x) * invS;
  }
  else if (mat.row1.y > mat.row2.z) {
    Scalar s = 2.0 * sqrt(1.0 + mat.row1.y - mat.row0.x - mat.row2.z);
    Scalar invS = 1.0 / s;
    if( Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor( s ) )
      Math_reportBadDivisor( s, "Quat.setFromMat33" );
    this.w = (mat.row0.z - mat.row2.x) * invS;
    this.v.x = (mat.row0.y + mat.row1.x) * invS;
    this.v.y = 0.25 * s;
    this.v.z = (mat.row1.z + mat.row2.y) * invS;
  }
  else {
    Scalar s = 2.0 * sqrt(1.0 + mat.row2.z - mat.row0.x - mat.row1.y);
    Scalar invS = 1.0 / s;
    if( Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor( s ) )
      Math_reportBadDivisor( s, "Quat.setFromMat33" );
    this.w = (mat.row1.x - mat.row0.y) * invS;
    this.v.x = (mat.row0.z + mat.row2.x) * invS;
    this.v.y = (mat.row1.z + mat.row2.y) * invS;
    this.v.z = 0.25 * s;
  }
  this.setUnit();
  return this;
}

/// Set the quaternion to the rotation required to rotate the source
/// vector to the destination vector
/// Function taken from the 'Game Programming Gems' article 'The Shortest Arc Quaternion' by Stan Melax
/// Both vectors must be units.
function Quat Quat.setFrom2Vectors!(in Vec3 sourceDirVec, in Vec3 destDirVec, Boolean arbitraryIfAmbiguous) {
  Scalar val = sourceDirVec.dot(destDirVec) + 1;
  if( val > 0.0 )
    val = sqrt(val * 2);
  if (val <= DIVIDEPRECISION) {
    if(arbitraryIfAmbiguous) {
      // the vectors pointed in opposite directions OR they are not unit vectors.
      // creating the quaternion is ambiguous (many answers)
      //Take any orthogonal vector as an intermediate step
      Vec3 ortho(sourceDirVec.y, sourceDirVec.z, sourceDirVec.x);
      ortho = ortho.cross(sourceDirVec).unit();
      //Important: call with arbitraryIfAmbiguous = false, else it could recurse infinitely if sourceDirVec or destDirVec was (0,0,0)
      Quat q1, q2;
      q1.setFrom2Vectors(sourceDirVec, ortho, false);
      q2.setFrom2Vectors(ortho, destDirVec, false);
      this = q1 * q2;
      return this;
    }
    if( Fabric_Guarded && !Fabric_OnGPU )Math_reportBadDivisor( val, "Quat.setFromMat33: ambiguous answer" );
  }
  this.v = sourceDirVec.cross(destDirVec) / val;
  this.w = val / 2;
  return this;
}

/// Overload for previous setFrom2Vectors method, avoid arbitrary rotations.
inline Quat Quat.setFrom2Vectors!(in Vec3 sourceDirVec, in Vec3 destDirVec) {
  this.setFrom2Vectors(sourceDirVec, destDirVec, false);
  return this;
}

/// Set the quat to represent the direction as the Z axis
/// and the upvector pointing along the XY plane.
inline Quat Quat.setFromDirectionAndUpvector!(in Vec3 direction, in Vec3 upvector) {
  Vec3 zaxis = direction.unit();
  Vec3 yaxis = zaxis.cross(upvector.unit()).cross(zaxis).unit();
  Vec3 xaxis = yaxis.cross(zaxis).unit();
  this.setFromMat33(Mat33(xaxis, yaxis, zaxis).transpose());
  return this;
}

/// Returns true if this quaternion is equal
/// to another one
inline Boolean Quat.equal (in Quat other) {
  return this.v.equal(other.v) && this.w == other.w;
}

/// Returns true if this quaternion is
/// almost equal to another one (given a precision)
inline Boolean Quat.almostEqual(in Quat other, in Scalar precision) {
  return
      this.v.almostEqual(other.v, precision) &&
      (abs(this.w - other.w) < precision);
}

/// Returns true if this quaternion is
/// almost equal to another one (using a default precision)
inline Boolean Quat.almostEqual(in Quat other) {
  return this.almostEqual(other, PRECISION);
}

/// Equals operator
inline Boolean == (Quat a, Quat b) {
  return a.v == b.v && a.w == b.w;
}

/// Not equals operator
inline Boolean != (Quat a, Quat b) {
  return a.v != b.v || a.w != b.w;
}

/// Hash for this value
inline UInt32 Quat.hash() {
  return this.v.hash() ^ this.w.hash();
}

/// Adds two quaternions
inline Quat + (in Quat a, in Quat b) {
  return Quat(a.w + b.w, a.v + b.v);
}

/// Adds another quaternion to this one
inline Quat. += (in Quat b) {
  this = this + b;
}

/// Subtracts two quaternions
inline Quat - (in Quat a, in Quat b) {
  return Quat(a.w - b.w, a.v - b.v);
}

/// Subtracts another quaternion from this one
inline Quat. -= (in Quat b) {
  this = this - b;
}

/// Multiplies two quaternions
/// \dfgPresetDefault b {"v": {"x": 0, "y": 0, "z": 0}, "w": 1 }
inline Quat * (in Quat a, in Quat b) {
  return Quat(a.w * b.w - a.v.dot(b.v), a.v.cross(b.v) + (a.w * b.v) + (a.v * b.w));
}

/// Multiplies this quaternion with another one
/// \dfgPresetDefault b {"v": {"x": 0, "y": 0, "z": 0}, "w": 1 }
inline Quat. *= (in Quat b) {
  this = this * b;
}

/// Multiplies a scalar with a quaternion
/// \dfgPresetDefault a 1
inline Quat * (in Scalar a, in Quat b) {
  return Quat(a * b.w, a * b.v);
}

/// Multiplies a quaternion with a scalar
/// \dfgPresetDefault b 1
inline Quat * (in Quat a, in Scalar b) {
  return Quat(a.w * b, a.v * b);
}

/// Multiplies this quaternion with a scalar
/// \dfgPresetDefault b 1
inline Quat. *= (in Scalar b) {
  this = this * b;
}

/// Returns the division of two quaternions
/// \dfgPresetDefault b {"v": {"x": 0, "y": 0, "z": 0}, "w": 1 }
inline Quat / (in Quat a, in Quat b) {
  return Quat(a.w * b.w + a.v.dot(b.v), (a.v * b.w) - (a.w * b.v) - a.v.cross(b.v));
}

/// Divides this quaternion by another one
/// \dfgPresetDefault b {"v": {"x": 0, "y": 0, "z": 0}, "w": 1 }
inline Quat. /= (in Quat b) {
  this = this / b;
}

/// Returns the division of a quaternion and a scalar
/// \dfgPresetDefault b 1
inline Quat / (Quat a, Scalar b) {
  if( Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor( b ) )
    Math_reportBadDivisor( b, "Quat.divide" );
  return a * (1.0 / b);
}

/// Divides this quaternion by a scalar
/// \dfgPresetDefault b 1
inline Quat. /= (in Scalar b) {
  this = this / b;
}

/// Overload method for the add operator
inline Quat Quat.add(Quat other) {
  return this + other;
}

/// Overload method for the subtract operator
inline Quat Quat.subtract(Quat other) {
  return this - other;
}

/// Overload method for the multiply operator
/// \dfgPresetDefault other {"v": {"x": 0, "y": 0, "z": 0}, "w": 1 }
inline Quat Quat.multiply(Quat other) {
  return this * other;
}

/// Overload method for the divide operator
/// \dfgPresetDefault other {"v": {"x": 0, "y": 0, "z": 0}, "w": 1 }
inline Quat Quat.divide(Quat other) {
  return this / other;
}

/// Returns the product of this quaternion and a scalar
/// \dfgPresetDefault other 1
inline Quat Quat.multiplyScalar(Scalar other) {
  return this * other;
}

/// Returns the division of this quaternion and a scalar
/// \dfgPresetDefault other 1
inline Quat Quat.divideScalar(Scalar other) {
  return this / other;
}

/// Rotates a vector by this quaternion.
/// Don't forget to normalize the quaternion unless
/// you want axial translation as well as rotation.
inline Vec3 Quat.rotateVector(in Vec3 v) {
  Quat vq = Quat(0.0, v);
  Quat pq = this * vq * this.conjugate();
  return pq.v;
}

/// Returns the dot product of this quaternion and another one
inline Scalar Quat.dot(in Quat other) {
  return this.w * other.w + this.v.dot(other.v);
}

/// Returns the conjugate of this quaternion
inline Quat Quat.conjugate() {
  return Quat(this.w, this.v.negate());
}

/// Returns the squared length of this quaternion
inline Scalar Quat.lengthSquared() {
  return this.w * this.w + this.v.lengthSquared();
}

/// Returns the length of this quaternion
inline Scalar Quat.length() {
  return sqrt(this.lengthSquared());
}

/// Returns a unit quaternion of this one
inline Quat Quat.unit() {
  Scalar n = this.length();
  if( Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor( n ) )
    Math_reportBadDivisor( n, "Quat.unit" );
  return this / n;
}

/// Returns a unit quaternion of this one, no error reported if cannot be made unit
inline Quat Quat.unit_safe() {
  Scalar n = this.length();
  if( Math_badDivisor( n ) )
    return Quat();
  return this / n;
}

/// Sets this quaternion to a unit quaternion and returns
/// the previous length
inline Scalar Quat.setUnit!() {
  Scalar n = this.length();
  if( Fabric_Guarded && !Fabric_OnGPU && Math_badDivisor( n ) )
    Math_reportBadDivisor( n, "Quat.setUnit" );
  this /= n;
  return n;
}

/// Returns a inverse quaternion of this one
inline Quat Quat.inverse() {
  return this.unit().conjugate();
}

/// Matches this quaternion with another one ensuring that they are 
/// withing the same hemisphere. The delta between Quaternion values
/// is the shortest path over the hypersphere.
inline Quat.matchHemisphere!(in Quat other) {
  if(this.dot(other) < 0.0){
    this = Quat(-this.w, this.v.negate());
  }
}

/// Deprecated method left for compatability. Method name was obscure and is
/// now 'matchHemisphere()'.
/// \dfgPresetOmit
inline Quat.alignWith!(in Quat other) {
  this.matchHemisphere(other);
}

/// Returns the angle of this quaternion (in radians)
inline Scalar Quat.getAngle() {
  return acos(this.w) * 2.0;
}

/// Returns the X axis of this quaternion
inline Vec3 Quat.getXaxis() {
  Vec3 temp;
  Scalar xy = this.v.x * this.v.y; Scalar xz = this.v.x * this.v.z;
  Scalar yy = this.v.y * this.v.y; Scalar yw = this.v.y * this.w;
  Scalar zz = this.v.z * this.v.z; Scalar zw = this.v.z * this.w;

  temp.x = 1.0 - 2.0 * (zz + yy);
  temp.y = 2.0 * (xy + zw);
  temp.z = 2.0 * (xz - yw);
  return temp;
}

/// Returns the Y axis of this quaternion
inline Vec3 Quat.getYaxis() {
  Vec3 temp;
  Scalar xx = this.v.x * this.v.x; Scalar xy = this.v.x * this.v.y; Scalar xw = this.v.x * this.w;
  Scalar yz = this.v.y * this.v.z;
  Scalar zz = this.v.z * this.v.z; Scalar zw = this.v.z * this.w;

  temp.x = 2.0 * (xy - zw);
  temp.y = 1.0 - 2.0 * (zz + xx);
  temp.z = 2.0 * (yz + xw);
  return temp;
}

/// Returns the Z axis of this quaternion
inline Vec3 Quat.getZaxis() {
  Vec3 temp;
  Scalar xx = this.v.x * this.v.x; Scalar xz = this.v.x * this.v.z; Scalar xw = this.v.x * this.w;

  Scalar yy = this.v.y * this.v.y; Scalar yz = this.v.y * this.v.z; Scalar yw = this.v.y * this.w;

  temp.x = 2.0 * (yw + xz);
  temp.y = 2.0 * (yz - xw);
  temp.z = 1.0 - 2.0 * (yy + xx);
  return temp;
}

/// Returns an axis and angle from this quaternion
Quat.getAxisAndAngle( out Vec3 axis, out Scalar angle ) {
  // Ensure it's normalized
  Quat q = this;
  if( q.w > 0.99999 || q.w < 1.00001 )
    q.setUnit();
  angle = 2 * acos( q.w );
  Scalar s = sqrt( 1 - q.w * q.w );
  if (s < DIVIDEPRECISION) {
    // axis not important (no rotation)
    axis.x = 1;
    axis.y = 0;
    axis.z = 0;
  } else {
    // normalize
    axis.x = q.v.x / s;
    axis.y = q.v.y / s;
    axis.z = q.v.z / s;
  }
}

/// Reflects this Quaternion according to the axis provided.
/// \param axisIndex An integer with value of 0 for the X axis, 1 for the Y axis, and 2 for the Z axis.
inline Quat.mirror!(in Integer axisIndex) {
  Scalar tempW = this.w;
  switch (axisIndex) {
    case 0: this.w =  this.v.y; this.v.set(this.v.z, tempW, this.v.x); break;
    case 1: this.w = -this.v.x; this.v.set(-tempW, this.v.z, this.v.y); break;
    case 2: this.w = -this.w; this.v.set(this.v.x, this.v.y, - this.v.z); break;
  }
}

/// Returns this quaternion as a 3x3 rotation matrix
inline Mat33 Quat.toMat33() {
  Mat33 temp;

  Scalar xx = this.v.x * this.v.x, xy = this.v.x * this.v.y, xz = this.v.x * this.v.z, xw = this.v.x * this.w;
  Scalar yy = this.v.y * this.v.y, yz = this.v.y * this.v.z, yw = this.v.y * this.w;
  Scalar zz = this.v.z * this.v.z, zw = this.v.z * this.w;

  temp.row0.x = 1.0 - 2.0 * (zz + yy);
  temp.row1.x = 2.0 * (xy + zw);
  temp.row2.x = 2.0 * (xz - yw);

  temp.row0.y = 2.0 * (xy - zw);
  temp.row1.y = 1.0 - 2.0 * (zz + xx);
  temp.row2.y = 2.0 * (yz + xw);

  temp.row0.z = 2.0 * (yw + xz);
  temp.row1.z = 2.0 * (yz - xw);
  temp.row2.z = 1.0 - 2.0 * (yy + xx);

  return temp;
}

/// Returns this quaternion as a Euler rotation
/// giving a rotation order
function Euler Quat.toEuler( RotationOrder rotationOrder ) {
  Quat unit = this.unit();//We cannot modify 'this'...
  Vec3 ordered;
  if (rotationOrder.isXYZ()) {
    ordered.set(unit.v.x,-unit.v.z,unit.v.y);
  }
  else if (rotationOrder.isYZX()) {
    ordered.set(unit.v.y,-unit.v.x,unit.v.z);
  }
  else if (rotationOrder.isZXY()) {
    ordered.set(unit.v.z,-unit.v.y,unit.v.x);
  }
  else if (rotationOrder.isXZY()) {
    ordered.set(unit.v.x,unit.v.y,unit.v.z);
  }
  else if (rotationOrder.isZYX()) {
    ordered.set(unit.v.z,unit.v.x,unit.v.y);
  }
  else if (rotationOrder.isYXZ()) {
    ordered.set(unit.v.y,unit.v.z,unit.v.x);
  }

  Vec3 euler;
  Scalar test = ordered.x*ordered.y + ordered.z*unit.w;
	if (test > 0.49999) { // singularity at north pole
		euler.y = 2.0 * atan2(ordered.x, unit.w);
		euler.z = HALF_PI;
		euler.x = 0.0;
	}
  else if (test < -0.49999) { // singularity at south pole
		euler.y = -2.0 * atan2(ordered.x,unit.w);
		euler.z = -HALF_PI;
		euler.x = 0.0;
	}
  else {
    Scalar sqx = ordered.x*ordered.x;
    Scalar sqy = ordered.y*ordered.y;
    Scalar sqz = ordered.z*ordered.z;
    euler.y = atan2(2.0*ordered.y*unit.w-2.0*ordered.x*ordered.z , 1.0 - 2.0*sqy - 2.0*sqz);
	  euler.z = asin(2.0*test);
	  euler.x = atan2(2.0*ordered.x*unit.w-2.0*ordered.y*ordered.z , 1.0 - 2.0*sqx - 2.0*sqz);
  }

  if (rotationOrder.isXYZ()) {
    return Euler(euler.x,euler.z,-euler.y,rotationOrder);
  }
  else if (rotationOrder.isYZX()) {
    return Euler(-euler.y,euler.x,euler.z,rotationOrder);
  }
  else if (rotationOrder.isZXY()) {
    return Euler(euler.z,-euler.y,euler.x,rotationOrder);
  }
  else if (rotationOrder.isXZY()) {
    return Euler(euler.x,euler.y,euler.z,rotationOrder);
  }
  else if (rotationOrder.isZYX()) {
    return Euler(euler.y,euler.z,euler.x,rotationOrder);
  }
  else if (rotationOrder.isYXZ()) {
    return Euler(euler.z,euler.x,euler.y,rotationOrder);
  }
  return Euler(0.0,0.0,0.0);
}

/// Returns this quaternion as a Euler angles using the rotationorder XYZ
inline Vec3 Quat.toEulerAngles(RotationOrder order) {
  return this.toEuler(order).getAngles();
}

/// Returns this quaternion as a Euler angles using the rotationorder XYZ
inline Vec3 Quat.toEulerAngles() {
  return this.toEulerAngles(RotationOrder(0));
}

/// interpolates two quaternions spherically (slerp)
/// given a scalar blend value (0.0 to 1.0).
/// \dfgPresetRange t (0.0, 1.0)
/// \note this and q2 should be unit Quaternions
function Quat Quat.sphericalLinearInterpolate(in Quat q2, in Scalar t) {
  Scalar angle = this.dot(q2);
  Quat _q2 = q2;
  if (angle < 0.0) {
    _q2.v = _q2.v.negate();
    _q2.w *= - 1.0;
  }

  Quat r;

  Scalar cosHalfTheta = this.dot(_q2);

  if (abs(cosHalfTheta) < 1.0) {
    Scalar halfTheta = acos(cosHalfTheta);
    Scalar sinHalfTheta = sqrt(1.0 - cosHalfTheta * cosHalfTheta);

    if (abs(sinHalfTheta) < DIVIDEPRECISION) {
      r = Quat((this.w + _q2.w) * 0.5, (this.v + _q2.v) * 0.5);
    }
    else {
      Scalar rA = sin((1 - t) * halfTheta) / sinHalfTheta;
      Scalar rB = sin(t * halfTheta) / sinHalfTheta;

      r = Quat(this.w * rA + _q2.w * rB, this.v * rA + _q2.v * rB);
    }
  }
  else {
    // They're the same
    r = this;
  }
  return r.unit();
}

/// Interpolates two quaternions lineally (lerp)
/// with a given blend value (0.0 to 1.0).
/// \note The interpolation of the 2 quaternions will result acceleration and deceleration. Use :kl-ref:`Quat.sphericalLinearInterpolate` for an interpolation that does not introduce acceleration.
function Quat Quat.linearInterpolate(in Quat other, in Scalar t) {
  Scalar angle = this.dot(other);
  Quat _other = other;
  if (angle < 0.0) {
    _other.v = _other.v.negate();
    _other.w *= - 1.0;
  }

  Quat q;
  q.v = this.v.linearInterpolate(_other.v, t);
  q.w = this.w + ((_other.w - this.w) * t);
  return q.unit();
}

/// \dfgPresetColor Color(99, 129, 92)
/// \dfgPresetFolder Random
/// \dfgCreateArrayPreset false
/// \dfgPresetDefault min 0.0
/// \dfgPresetDefault min 1.0
inline Quat mathRandomQuat(Size seed, Size index, Float32 min, Float32 max) {
  Quat result;
  result.v.x = mathRandomScalar(seed, index+0, -1, 1);
  result.v.y = mathRandomScalar(seed, index+1, -1, 1);
  result.v.z = mathRandomScalar(seed, index+2, -1, 1);
  result.v.setUnit();
  result.w = mathRandomScalar(seed, index+3, min, max);
  return result;
}

/// \dfgPresetColor Color(99, 129, 92)
/// \dfgCreateArrayPreset false
function Quat Quat.log() {
  Quat qLog;
  Scalar scale = this.v.length();
  Scalar theta = atan2(scale, this.w);
  if (scale > 0.0)
      scale = theta / scale;
  qLog.v.x = scale * this.v.x;
  qLog.v.y = scale * this.v.y;
  qLog.v.z = scale * this.v.z;
  qLog.w = 0.0;
  return qLog;
}

/// \dfgPresetColor Color(99, 129, 92)
/// \dfgCreateArrayPreset false
function Quat Quat.exp() {
  Quat qExp;
  Scalar theta = this.v.length();
  Scalar scale = 1.0;
  if (theta > 1.0e-20) 
    scale = sin(theta) / theta;
  qExp.v.x = scale * this.v.x;
  qExp.v.y = scale * this.v.y;
  qExp.v.z = scale * this.v.z;
  qExp.w = cos(theta);
  return qExp;
}

/// \dfgPresetColor Color(99, 129, 92)
/// \dfgCreateArrayPreset false
function Quat Quat.pow(Scalar s) {
  return (s * this.log()).exp();
}