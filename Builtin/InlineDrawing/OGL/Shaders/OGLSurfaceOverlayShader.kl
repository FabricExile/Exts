/*
 *  Copyright (c) 2010-2017 Fabric Software Inc. All rights reserved.
 */

require Math;
require Util;

/**
\versionadded 1.12.0

The OGLSurfaceOverlayShader is used for rendering surface attributes as a semitransparent colored overlay on top of existing geometry.
It offsets the overlay geometry in the -Z space to ensure no z-fighting occurs between the underlying geometry and the overlay.

This shader is often used to display paintable weightmaps over the top of geometry in the viewport.  

\rst
Uniforms
----------
==================== ============ ===================================== ==================================
Name                 Type         Description                           Default
==================== ============ ===================================== ==================================
u_color              Color        Color of the map drawn                Color(1.0, 0.0, 0.0)
u_light0Position     Vec3         Position of the first light           Position of the camera
u_light0Color        Color        Color of the first light              Color(1.0, 1.0, 1.0)
u_normalIsTangent    Integer      Flag defining normal type             0
u_doubleSided        Integer      Flag defining backface shading model  0
==================== ============ ===================================== ==================================

Attributes
----------
=============== ============ =====================================
Name            Type         Description
=============== ============ =====================================
positions       Vec3         Geometry positions
normals         Vec3         Geometry normals
weightMap       Scalar       Geometry attribute being used to color the surface. 
=============== ============ =====================================
\endrst

\example
  InlineDrawing draw = OGLInlineDrawing();
  InlineShader shader = draw.registerShader(OGLSurfaceOverlayShader);
  InlineMaterial mat = shader.getOrCreateMaterial('blue');
  mat.setUniform('u_color', Color(0.0, 0.0, 1.0));
\endexample
\dfgPresetOmit
*/
object OGLSurfaceOverlayShader: OGLSurfaceShader {};

/// default constructor
function OGLSurfaceOverlayShader() {
  // Avoid calling the init method if this default constructor is being invoked 
  // during the construction of a derived object.
  if(this.type() == OGLSurfaceOverlayShader)
    this.init(this.type(), 'weightMap');
}

/// default constructor taking a name
function OGLSurfaceOverlayShader(String name) {
  this.init(name, 'weightMap');
}

/// default constructor taking a name and weightmap name. 
function OGLSurfaceOverlayShader(String name, String weightMapName) {
  this.init(name, weightMapName);
}


/// Initialize the shader using a name and weightmap name. 
function OGLSurfaceOverlayShader.init!(String name, String weightMapName) {
  this.name = name;
  this.setVertexCode("
      #version 120 // We require OpenGL 2.1 for InlineDrawing, which supports GLSL 1.2
      
      uniform mat4 u_viewMatrix;
      uniform mat4 u_projection;
      uniform mat4 u_modelMatrix;
      uniform mat3 u_normalMatrix;

      uniform vec3 u_light0Position;
      uniform vec4 u_light0Color;

      attribute vec4 positions;
      attribute vec3 normals;
      attribute float XXX_WEIGHTMAP_XXX;

      varying vec3 v_normal;
      varying vec4 v_position;
      varying vec3 v_lightDir;
      varying vec4 v_lightColor;
      varying vec3 v_eyeVec;
      varying float v_XXX_WEIGHTMAP_XXX;

      void main(){
        v_normal = u_normalMatrix * normals;
        v_position = u_modelMatrix * positions;
        vec4 v_positionInv = (u_viewMatrix * vec4(v_position.xyz, 1.0));
        gl_Position = u_projection * v_positionInv;
  
        v_lightDir = (u_viewMatrix * vec4(u_light0Position, 1.0)).xyz - v_positionInv.xyz;
        v_lightColor = u_light0Color;
        v_eyeVec = -v_positionInv.xyz;
        v_XXX_WEIGHTMAP_XXX = XXX_WEIGHTMAP_XXX;
      }
    ".replace('XXX_WEIGHTMAP_XXX', weightMapName));
  this.setFragmentCode("
      #version 120 // We require OpenGL 2.1 for InlineDrawing, which supports GLSL 1.2
      
      uniform vec4 u_color;
      uniform int u_normalIsTangent;
      uniform int u_doubleSided;

      varying vec3 v_normal;
      varying vec4 v_position;
      varying vec3 v_lightDir;
      varying vec4 v_lightColor;
      varying vec3 v_eyeVec;
      varying float v_XXX_WEIGHTMAP_XXX;

      void main() {
        vec4 final_color = vec4(0.0, 0.0, 0.0, 0.0);
        

        vec3 N;
        if(u_normalIsTangent == 0)
          N = normalize(v_normal);
        else
        {
          N = normalize(cross(v_normal, cross(v_normal, v_eyeVec)));
        }
        if(dot(N, v_eyeVec) < 0.0)
          N = -N;
        vec3 L = normalize(v_lightDir);

        float lambertTerm = dot(N,L);
        if(u_doubleSided != 0)
          lambertTerm = abs(lambertTerm);
        
        if(lambertTerm > 0.0)
        {
          final_color += v_lightColor * (v_XXX_WEIGHTMAP_XXX * u_color) * lambertTerm; 
        }
        final_color.w = v_XXX_WEIGHTMAP_XXX;
        gl_FragColor = final_color;
      }
    ".replace('XXX_WEIGHTMAP_XXX', weightMapName));  

  this.defineUniform(InlineUniform("u_viewMatrix", Mat44()));
  this.defineUniform(InlineUniform("u_projection", Mat44()));
  this.defineUniform(InlineUniform("u_modelMatrix", Mat44()));
  this.defineUniform(InlineUniform("u_normalMatrix", Mat33()));

  this.defineUniform(InlineUniform("u_light0Position", Vec3(100, 100, 100)));
  this.defineUniform(InlineUniform("u_light0Color", Color(1.0, 1.0, 1.0)));

  this.defineUniform(InlineUniform("u_color", Color(1.0, 0.0, 0.0)));
  this.defineUniform(InlineUniform("u_normalIsTangent", Integer(0)));
  this.defineUniform(InlineUniform("u_doubleSided", Integer(0)));

  this.defineAttribute('positions', "Vec3");
  this.defineAttribute('normals', "Vec3");
  this.defineAttribute(weightMapName, "Scalar");
}

/// called by the InlineDrawing, should not be called manually
/// \internal
//function Boolean OGLInlineShader.draw!(io DrawContext context) {
//function Boolean OGLSurfaceShader.draw!(io DrawContext context) {
function Boolean OGLSurfaceOverlayShader.draw!(io DrawContext context) {

  if(this.getName() == 'debug')
    report('OGLSurfaceOverlayShader.draw '+this.getName());

  // enable overlay and blending 
  glDepthFunc (GL_LEQUAL);
  glEnable(GL_BLEND);
  glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);
  
  glEnable(GL_POLYGON_OFFSET_FILL);
  glPolygonOffset(-1.0,-1.0);

  Boolean result = this.parent.draw(context);

  glDisable(GL_POLYGON_OFFSET_FILL);
  glDisable(GL_BLEND);

  return result;
}
